grammar de.sebinside.dcp.dsl.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.sebinside.de/dcp/dsl/DSL"

// throws an excpetion: Couldn't resolve reference to EPackage 'http://www.palladiosimulator.org/pcm/dataprocessing/characteristics/0.1/'.
// import "http://www.palladiosimulator.org/pcm/dataprocessing/characteristics/0.1/" as characteristics

// Import approach, not working at all
//CharacteristicsType returns characteristics::EnumCharacteristicType:
//	entityName = ID '.' literal = ID;


Model:
	(elements+=AbstractElement)*;

AbstractElement:
	DataType | ValueSet | CharacteristicsType | CharacteristicClass | Constraint;


DataType:
	'datatype' name=ID;

ValueSet:
	'valueset' name=ID AssignementOperator '[' members+=ID (',' members+=ID)* ']';

CharacteristicsType:
	AttributeType | PropertyType;

AttributeType:
	'attribute' name=ID AssignementOperator valueset=[ValueSet];

PropertyType:
	'property' name=ID AssignementOperator valueset=[ValueSet];

CharacteristicClass:
	'class' name=ID '{' members+=CharacteristicSelector (',' members+=CharacteristicSelector)* '}';

CharacteristicSelector:
	ref=[CharacteristicsType] '.'
	(((negated?='!')? literals+=ID) |
	('[' literals+=ID ((',' literals+=ID)* | (conjuncted?='&' literals+=ID)*) ']'));

Constraint:
	'constraint' name=STRING '{' rule=Rule '}';

Rule:
	dataSelectors+=DataSelector ('&' dataSelectors+=DataSelector)*
	statement=Statement
	destinationSelectors+=DestinationSelector ('&' destinationSelectors+=DestinationSelector)*;

DataSelector:
	AttributeSelector | AttributeClassSelector;

AttributeSelector:
	'data.attribute.' ref=CharacteristicSelector;

AttributeClassSelector:
	'data.class.' (negated?='!')? ref=[CharacteristicClass];

DestinationSelector:
	PropertySelector | PropertyClassSelector;

PropertySelector:
	'node.property.' ref=CharacteristicSelector;

PropertyClassSelector:
	'node.class.' (negated?='!')? ref=[CharacteristicClass];

Statement:
	modality=StatementModality type=StatementType;

StatementType:
	name='FLOWS';

StatementModality:
	name='NEVER';

AssignementOperator:
	'<-';