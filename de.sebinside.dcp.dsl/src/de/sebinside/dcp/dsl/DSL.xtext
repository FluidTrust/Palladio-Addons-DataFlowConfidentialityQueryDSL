grammar de.sebinside.dcp.dsl.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.sebinside.de/dcp/dsl/DSL"

// Nested EPackage has to be referred to by platform:/resource URI
// https://sdqweb.ipd.kit.edu/wiki/Importing_nested_EPackages_in_Xtext
import "platform:/resource/org.palladiosimulator.pcm.dataprocessing/model/dataprocessing.ecore#//characteristics" as
characteristics

Model:
	(elements+=AbstractElement)*;

AbstractElement:
	Include | CharacteristicType | CharacteristicClass | Constraint | SL_COMMENT;

CharacteristicType:
	'type' name=ID ':' ref=[characteristics::CharacteristicType];

CharacteristicClass:
	'class' name=ID '{' members+=CharacteristicTypeSelector (',' members+=CharacteristicTypeSelector)* '}';

CharacteristicTypeSelector:
	ref=[CharacteristicType] '.'
	(((negated?='!')? literals+=[characteristics::EnumCharacteristicLiteral]) |
	('[' literals+=[characteristics::EnumCharacteristicLiteral]
	(',' literals+=[characteristics::EnumCharacteristicLiteral])*
	']'));

Include:
	'import' importURI=STRING;

Constraint:
	'constraint' name=ID '{' rule=Rule '}';

Rule:
	dataSelectors+=DataSelector ('&' dataSelectors+=DataSelector)*
	statement=Statement
	destinationSelectors+=DestinationSelector ('&' destinationSelectors+=DestinationSelector)*;

DataSelector:
	AttributeSelector | AttributeClassSelector;

AttributeSelector:
	'data.attribute.' ref=CharacteristicTypeSelector;

AttributeClassSelector:
	'data.class.' ref=[CharacteristicClass];

DestinationSelector:
	PropertySelector | PropertyClassSelector;

PropertySelector:
	'node.property.' ref=CharacteristicTypeSelector;

PropertyClassSelector:
	'node.class.' ref=[CharacteristicClass];

Statement:
	modality=StatementModality type=StatementType;

StatementType:
	name='FLOWS';

StatementModality:
	name='NEVER';